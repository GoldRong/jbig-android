
version = android.defaultConfig.versionName

def siteUrl = 'https://github.com/suzp1984/jbig-android'      // Homepage URL of the library
def gitUrl = 'https://github.com/suzp1984/jbig-android.git'   // Git repository URL

group = "lib.jacob.org.lib"

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'jbig library aar project for android'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'GNU GENERAL PUBLIC LICENSE, Version 3'
                        url 'http://www.gnu.org/licenses/gpl.txt'
                    }
                }
                developers {
                    developer {
                        id 'suzp1984'
                        name 'suzp1984'
                        email 'suzp1984@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    // options.encoding = 'UTF-8'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "jbigAndroid"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["GPL-3.0"]
        publish = true
    }
}